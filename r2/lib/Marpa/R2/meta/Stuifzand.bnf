# Copyright 2012 Jeffrey Kegler
# This file is part of Marpa::R2.  Marpa::R2 is free software: you can
# redistribute it and/or modify it under the terms of the GNU Lesser
# General Public License as published by the Free Software Foundation,
# either version 3 of the License, or (at your option) any later version.
#
# Marpa::R2 is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser
# General Public License along with Marpa::R2.  If not, see
# http://www.gnu.org/licenses/.

:start ::= rules
rules ::= rule+
rule ::= start_rule | empty_rule | priority_rule | quantified_rule | discard_rule
start_rule ::= (<kwc start> <op declare>) symbol
priority_rule ::= lhs <op declare> priorities
empty_rule ::= lhs <op declare> adverb_list
quantified_rule ::= lhs <op declare> single_symbol quantifier adverb_list
discard_rule ::= (<kwc discard> <op declare match>) single_symbol
<op declare> ::= <op declare bnf> | <op declare match>
priorities ::= alternatives+
    separator => <op tighter> proper => 1
alternatives ::= alternative+
    separator => <op eq pri> proper => 1
alternative ::= rhs adverb_list
adverb_list ::= adverb_item*
adverb_item ::=
      action
    | left_association | right_association | group_association
    | separator_specification | proper_specification

action ::= (<kw action> <op arrow>) action_name
left_association ::= (<kw assoc> <op arrow> <kw left>)
right_association ::= (<kw assoc> <op arrow> <kw right>)
group_association ::= (<kw assoc> <op arrow> <kw group>)
separator_specification ::= (<kw separator> <op arrow>) single_symbol
proper_specification ::= (<kw proper> <op arrow>) boolean

quantifier ::=
    <op star>
  | <op plus>

lhs ::= symbol_name
rhs ::= rhs_primary+
rhs_primary ::= single_symbol
rhs_primary ::= <kwc ws star>
rhs_primary ::= <kwc ws plus>
rhs_primary ::= <kwc ws>
rhs_primary ::= <kwc any>
rhs_primary ::= single_quoted_string
rhs_primary ::= <parenthesized rhs primary list>
<parenthesized rhs primary list> ::= (<op lparen>) rhs_primary_list (<op rparen>)
rhs_primary_list ::= rhs_primary+
single_symbol ::=
    symbol
  | character_class
symbol ::= symbol_name
symbol_name ::= bare_name | reserved_word
symbol_name ::= bracketed_name
action_name ::= bare_name | reserved_action_name

reserved_word ::= <kw action> | <kw assoc> | <kw separator> | <kw proper>
  | <kw left> | <kw right> | <kw group>
